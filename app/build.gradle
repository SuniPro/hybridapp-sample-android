plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.blackstone.belleforet"
        minSdk 26
        targetSdk 31
        versionCode 36
        versionName "1.3.0"

        archivesBaseName = "v${versionName}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [appName: "@string/app_name"]
        }
        debug {
            applicationIdSuffix ".dev"
            manifestPlaceholders = [appName: "개발용"]
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                def name = parent.project.getName()
                def buildType = variant.buildType.name
                def versionName = variant.versionName
                outputFileName = "${name}-${versionName}-${buildType}.apk"
            }
        }
    }

    flavorDimensions "server"
    productFlavors {
        // 벨포레 운영
        blackstonebelleforet {
            dimension "server"
            versionNameSuffix "-blackstonebelleforet"
            buildConfigField "String", "HOME_PAGE", "\"https://mser.blackstonebelleforet.com\""
            buildConfigField "String", "BASE_URL", "\"https://mser.blackstonebelleforet.com\""
        }
        // 벨포레 개발
        domain {
            dimension "server"
            versionNameSuffix "-domain"
            buildConfigField "String", "HOME_PAGE", "\"https://ser.bsgrcc.com\""
            buildConfigField "String", "BASE_URL", "\"https://ser.bsgrcc.com\""
        }
        ip {
            dimension "server"
            versionNameSuffix "-ip"
            buildConfigField "String", "HOME_PAGE", "\"http://202.68.231.241:8080\""
            buildConfigField "String", "BASE_URL", "\"http://202.68.231.241:8080\""
        }
        home {
            dimension "server"
            versionNameSuffix "-home"
            buildConfigField "String", "HOME_PAGE", "\"http://192.168.35.82\""
            buildConfigField "String", "BASE_URL", "\"http://202.68.231.241:8080\""
        }
        office {
            dimension "server"
            versionNameSuffix "-office"
            buildConfigField "String", "HOME_PAGE", "\"http://192.168.43.144\""
            buildConfigField "String", "BASE_URL", "\"http://202.68.231.241:8080\""
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

}

dependencies {
    implementation project(":data")
    implementation project(":domain")

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    /* Hilt */
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    /* Jetpack */
    def lifecycle_version = "2.4.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    /* Coroutines */
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    /* Fragment */
    def fragment_version = "1.4.1"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    /* SwiperefreshLayout */
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    /* Ktor */
    implementation "io.ktor:ktor-client-gson:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"

    /* Kotlinx serialization */
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2")

    /* BoM for the Firebase platform */
    implementation platform('com.google.firebase:firebase-bom:29.1.0')

    /* Crashlytics and Analytics libraries */
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
}